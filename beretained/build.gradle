apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = project.libVersion
group = project.libGroup

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    resourcePrefix "wooodblock_beretained__"

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 23
        versionCode 1
        versionName version
    }

    buildTypes {
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.4.0'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name 'Simple library to help retaining non-Parcelable objects during Activity configuration change'
                url project.siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'DrBreen'
                        name 'Alexander Leontev'
                        email 'wryyy906@gmail.com'
                    }
                }
                scm {
                    connection project.gitUrl
                    developerConnection project.gitUrl
                    url project.siteUrl

                }
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += fileTree(dir: "$buildDir/intermediates/exploded-aar/", include:"**/classes.jar")
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    publish = true
    override = true

    pkg {
        repo = "maven"
        publicDownloadNumbers = true

        name = "BeRetained"
        desc = "BeRetained is a simple library that will handle non-parcelable instances saving for configuration changes."

        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = gitIssueTrackerUrl

        labels = ["android", "retained fragment", "configuration change"]

        licenses = ["Apache-2.0"]

        githubRepo = "DrBreen/BeRetained"
        githubReleaseNotesFile = "README.md"
    }
}
